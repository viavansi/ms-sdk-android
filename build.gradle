repositories {
    maven { url "https://maven.google.com" }
    jcenter()
    mavenCentral()
    maven { url 'https://repo1.maven.org/maven2' }
    maven { url "https://repositorio.viavansi.com/artifactory/libs-releases-local/" }
}

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
		abortOnError false
	}
	
	useLibrary 'org.apache.http.legacy'
}

configurations {
    deployerJars
}

dependencies {
	// Maven publishing
    deployerJars 'org.apache.maven.wagon:wagon-http:2.2'
    
    api 'com.google.code.gson:gson:2.2.+'

    api 'com.wordnik:swagger-annotations:1.5.1-M1'

    api 'com.squareup.okhttp3:okhttp:3.2.0'
    api 'se.akerfeldt:okhttp-signpost:1.1.0'
    api 'oauth.signpost:signpost-core:1.2.1.2'
    api 'org.apache.commons:commons-lang3:3.4'
}

// For Maven Repository submitting
task uploadRelease(type: Upload) {
    configuration = configurations.archives
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: releaseUrl) {
            authentication(userName: repoUsername, password: repoPassword)
        }
        pom.version = '3.7.53'
        pom.artifactId = "ms-sdk-android"
        pom.groupId = "com.viafirma"
    }
}

task uploadSnapshot(type: Upload) {
    configuration = configurations.archives
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        snapshotRepository(url: snapshotUrl) {
            authentication(userName: repoUsername, password: repoPassword)
        }
        pom.version = '3.7.53' + "-SNAPSHOT"
        pom.artifactId = "ms-sdk-android"
        pom.groupId = "com.viafirma"
    }
}

// This is the task to be used for every release.
task releaseVersion(dependsOn: ['clean', 'build', 'uploadRelease'])
build.mustRunAfter clean
uploadRelease.mustRunAfter build

task snapshotVersion(dependsOn: ['clean', 'build', 'uploadSnapshot'])
build.mustRunAfter clean
uploadSnapshot.mustRunAfter build
