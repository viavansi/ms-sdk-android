repositories {
    maven { url "https://maven.google.com" }
    mavenCentral()
    maven { url 'https://repo1.maven.org/maven2' }
    maven { url "https://repositorio.viavansi.com/artifactory/libs-releases-local/" }
}

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {

    useLibrary 'org.apache.http.legacy'
    namespace "com.viafirma.mobile.services.android"
    compileSdk 33

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
		abortOnError false
	}
    publishing {
        singleVariant("release") {
            withSourcesJar()
            // withJavadocJar()
        }
    }
}

configurations {
    deployerJars
}

dependencies {
	// Maven publishing
    deployerJars 'org.apache.maven.wagon:wagon-http:2.2'

    api 'com.google.code.gson:gson:2.2.+'

    api 'com.wordnik:swagger-annotations:1.5.1-M1'

    api 'com.squareup.okhttp3:okhttp:3.2.0'
    api 'se.akerfeldt:okhttp-signpost:1.1.0'
    api 'oauth.signpost:signpost-core:1.2.1.2'
    api 'org.apache.commons:commons-lang3:3.4'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.viafirma"
                artifactId = "ms-sdk-android"
                version = "3.7.85"
            }
            snapshot(MavenPublication) {
                from components.release
                groupId = "com.viafirma"
                artifactId = "ms-sdk-android"
                version = "3.7.85-SNAPSHOT"
            }
        }
        repositories {
            maven {
                name = 'release'
                url = releaseUrl
                credentials {
                    username = repoUsername
                    password = repoPassword
                }
            }
            maven {
                name = 'snapshot'
                url = snapshotUrl
                credentials {
                    username = repoUsername
                    password = repoPassword
                }
            }
        }
    }

    tasks.register("releaseVersion") {
        dependsOn("clean", "build", "publishReleasePublicationToReleaseRepository")
    }
    tasks.register("snapshotVersion") {
        dependsOn("clean", "build", "publishSnapshotPublicationToSnapshotRepository")
    }
}
